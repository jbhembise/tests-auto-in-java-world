import com.thoughtworks.gauge.gradle.GaugeTask // For Gauge

group 'jbhembise.testauto'
version '1.0-SNAPSHOT'
description 'Project to benchmark different frameworks/libraries in Java World'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.51" // For Spek framework
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0' // For test mutation (Pitest)
        classpath "net.saliman:gradle-cobertura-plugin:2.5.4" // For Cobertura
        classpath 'com.thoughtworks.gauge.gradle:gauge-gradle-plugin:+' // For Gauge
        classpath "com.tngtech.jgiven:jgiven-gradle-plugin:0.16.1" // For JGiven HTML reports
        classpath "net.serenity-bdd:serenity-gradle-plugin:1.1.1" // For Serenity BDD
    }
}

apply plugin: 'java'
apply plugin: 'groovy' // For Spock framework
apply plugin: 'kotlin' // For Spek framework
apply plugin: 'info.solidsoft.pitest' // For test mutation (Pitest)
apply plugin: "jacoco" // For Jacoco
apply plugin: 'net.saliman.cobertura' // For Cobertura
apply plugin: 'gauge' // For Gauge framework
apply plugin: 'com.tngtech.jgiven.gradle-plugin' // For JGiven
apply plugin: 'net.serenity-bdd.aggregator' // For Serenity BDD

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Lombok
    annotationProcessor "org.projectlombok:lombok:1.18.2"
    compile 'org.projectlombok:lombok:1.18.2'

    // Logback
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.11'

    // JUnit framework
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.2.0'
    testCompile group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.2.0'
    testCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.3.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-suite-api', version: '1.3.1'

    // JUnit data provider library
    testCompile group: 'com.tngtech.java', name: 'junit-dataprovider', version: '1.10.0'

    // TestNG framework
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'

    // Spock framework
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.15'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testCompile(group: 'com.athaydes', name: 'spock-reports', version: '1.6.1') {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }

    // Spek framework
    testCompile group: 'org.jetbrains.spek', name: 'spek-api', version: '1.1.5'
    testRuntime group: 'org.jetbrains.spek', name: 'spek-junit-platform-engine', version: '1.1.5'

    // Cucumber + Gherkin framework
    testCompile group: 'info.cukes', name: 'cucumber-java', version: '1.2.5'
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.2.5'
    testCompile group: 'info.cukes', name: 'cucumber-picocontainer', version: '1.2.5'
    testCompile group: 'net.masterthought', name: 'cucumber-reporting', version: '4.1.0'

    // JGiven framework
    testCompile group: 'com.tngtech.jgiven', name: 'jgiven-junit', version: '0.15.3'
    testCompile group: 'com.tngtech.jgiven', name: 'jgiven-html5-report', version: '0.15.3'

    // JBehave framework
    testCompile group: 'org.jbehave', name: 'jbehave-core', version: '4.3.5'

    // AssertJ library
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.10.0'

    // Google Truth library
    testCompile group: 'com.google.truth', name: 'truth', version: '0.42'

    // Jqwik
    testCompile "net.jqwik:jqwik:0.8.15"

    // Junit-Quickcheck
    testCompile 'com.pholser:junit-quickcheck-core:0.8.1'
    testCompile 'com.pholser:junit-quickcheck-generators:0.8.1'

    // Mockito
    testCompile 'org.mockito:mockito-core:2.1.0'

    // JMock
    testCompile 'org.jmock:jmock-junit4:2.9.0'
    testCompile 'org.jmock:jmock-legacy:2.9.0'

    // JMockit
    testCompile 'org.jmockit:jmockit:1.43'

    // EasyMock
    testCompile 'org.easymock:easymock:3.6'

    // Gauge
    testCompile 'com.thoughtworks.gauge:gauge-java:0.6.8'

    // Serenity BDD
    testCompile group: 'net.serenity-bdd', name: 'serenity-core', version: '1.1.1'
    testCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: '1.1.1'

    // QuickTheories
    testCompile group: 'org.quicktheories', name: 'quicktheories', version: '0.25'
}

// Default configuration for Gradle test tasks
tasks.withType(Test) {
    // log every test result
    testLogging {
        events "passed", "skipped", "failed"
    }

    // test summary (displayed at the end)
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

test {
    // enable JUnit5 tests
    useJUnitPlatform()

    // To make JMockit works !
    def jmockit = configurations.testCompile.files.find { it.name.contains("jmockit") }.absolutePath
    jvmArgs "-javaagent:$jmockit"

    // To generate JGiven HTML report at the end
    finalizedBy jgivenTestReport
}

task testNG(type: Test) {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

check.dependsOn(testNG)

task testSpock(type: Test) {
    include '**/spock/**'
}

task testSpek(type: Test) {
    include '**/spek/**'
    useJUnitPlatform()
}

task testCucumber(type: Test) {
    include '**/cucumber/**'
}

task testJBehave(type: Test) {
    include '**/jbehave/**'
}

task testJGiven(type: Test) {
    include '**/jgiven/**'
}

task testJUnit4(type: Test) {
    include '**/junit4/**'
}

task testJUnit5(type: Test) {
    include '**/junit5/**'
    useJUnitPlatform()
}

task testJqwik(type: Test) {
    include '**/jqwik/**'
    useJUnitPlatform()
}

task testJunitQuickCheck(type: Test) {
    include '**/junitquickcheck/**'
}

task testJMock(type: Test) {
    include '**/jmock/**'
}

task testMockito(type: Test) {
    include '**/mockito/**'
}

task testEasyMock(type: Test) {
    include '**/easymock/**'
}

pitest {
    threads = 4
    targetClasses = ['jbhembise.testauto.mutation.*']
    targetTests = ['jbhembise.testauto.mutation.*']
    outputFormats = ['HTML']
}

task testJMockit(type: Test) {
    include '**/jmockit/**'
    def jmockit = configurations.testCompile.files.find { it.name.contains("jmockit") }.absolutePath
    jvmArgs "-javaagent:$jmockit"
}

jacocoTestReport {
    reports {
        html.enabled true
    }
    dependsOn check
}

coberturaReport {
    dependsOn check
}

gauge {
    specsDir = 'src/test/resources/jbhembise/testauto/gauge'
    additionalFlags = '--verbose'
}

tasks.withType(GaugeTask) {
    def jmockit = configurations.testCompile.files.find { it.name.contains("jmockit") }.absolutePath
    jvmArgs "-javaagent:$jmockit"
    include '**/gauge/**'
}

task testGauge(type: GaugeTask) {
    doFirst {
        gauge {
            specsDir = 'src/test/resources/jbhembise/testauto/gauge'
            additionalFlags = '--verbose'
        }
    }
}

task testAssertions(type: Test) {
    include '**/assertions/**'
    useJUnitPlatform()
}

task testSerenityBDD(type: Test) {
    include '**/serenitybdd/**'
}

task testQuickTheories(type: Test) {
    include '**/quicktheories/**'
}
