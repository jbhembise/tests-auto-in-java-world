group 'jbhembise.testauto'
version '1.0-SNAPSHOT'
description 'Project to benchmark different frameworks/libraries in Java World'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.51" // For Spek framework
    }
}

apply plugin: 'java'
apply plugin: 'groovy' // For Spock framework
apply plugin: 'kotlin' // For Spek framework

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// Default configuration for Gradle test tasks
tasks.withType(Test) {
    // log every test result
    testLogging {
        events "passed", "skipped", "failed"
    }

    // test summary (displayed at the end)
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

test {
    // enable JUnit5 tests
    useJUnitPlatform()
}

task testNG(type: Test) {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

test.dependsOn(testNG)

task testSpock(type: Test) {
    include '**/spock/**'
}

task testSpek(type: Test) {
    include '**/spek/**'
    useJUnitPlatform()
}

task testCucumber(type: Test) {
    include '**/cucumber/**'
}

task testJBehave(type: Test) {
    include '**/jbehave/**'
}

task testJGiven(type: Test) {
    include '**/jgiven/**'
}

task testJUnit4(type: Test) {
    include '**/junit4/**'
}

task testJUnit5(type: Test) {
    include '**/junit5/**'
    useJUnitPlatform()
}

dependencies {
    // JUnit framework
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.2.0'
    testCompile group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.2.0'

    // TestNG framework
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'

    // Spock framework
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.15'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'

    // Spek framework
    testCompile group: 'org.jetbrains.spek', name: 'spek-api', version: '1.1.5'
    testRuntime group: 'org.jetbrains.spek', name: 'spek-junit-platform-engine', version: '1.1.5'

    // Cucumber + Gherkin framework
    testCompile group: 'info.cukes', name: 'cucumber-java', version: '1.2.5'
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.2.5'
    testCompile group: 'info.cukes', name: 'cucumber-picocontainer', version: '1.2.5'

    // JGiven framework
    testCompile group: 'com.tngtech.jgiven', name: 'jgiven-junit', version: '0.15.3'

    // JBehave framework
    testCompile group: 'org.jbehave', name: 'jbehave-core', version: '4.3.5'

    // AssertJ library
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.10.0'

    // JUnit data provider library
    testCompile group: 'com.tngtech.java', name: 'junit-dataprovider', version: '1.10.0'
}
